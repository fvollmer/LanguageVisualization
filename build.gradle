import de.itemis.mps.gradle.*

buildscript {
    repositories {
        maven { url 'https://projects.itemis.de/nexus/content/repositories/mbeddr' }
        mavenCentral()
    }
    dependencies {
        classpath 'de.itemis.mps:mps-gradle-plugin:1.2+'
    }
}

repositories {
    maven { url 'https://projects.itemis.de/nexus/content/repositories/mbeddr' }
    mavenCentral()
}

group 'LanguageVisualization'
description = 'UML style visualization for the structure of mps languages'

apply plugin: 'base'

// Require java 8
if(JavaVersion.current() != JavaVersion.VERSION_1_8){
    throw new GradleException("Found Java ${JavaVersion.current()}, but Java ${JavaVersion.VERSION_1_8} is requiered")
}

// Dependency versions
ext.mpsVersion = '2018.3'
ext.mbeddrPlatformVersion = '1.2.+'

ext.incrementalBuild = !project.hasProperty('disableIncrementalBuild')

configurations {
    projectDependencies
	ant_lib
	mps
}

dependencies {
	projectDependencies "com.mbeddr:platform:$mbeddrPlatformVersion"
	ant_lib "org.apache.ant:ant-junit:1.20.+"
	mps "com.jetbrains:mps:$mpsVersion"
}

task downloadDependencies(type: Copy) {
    dependsOn configurations.projectDependencies
	description 'Download dependencies of project'
    from {
        configurations.projectDependencies.resolve().collect { zipTree(it) }
	} into 'lib'
}

task downloadMPS(type: Copy) {
    dependsOn configurations.mps
	description 'Download mps'
    from {
        configurations.mps.resolve().collect { zipTree(it) }
	} into 'mps'
}


ant.importBuild('build.xml') { target -> 'ant-' + target }

tasks['ant-declare-mps-tasks'].dependsOn downloadMPS, downloadDependencies

defaultTasks 'ant-generate'